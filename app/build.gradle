plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 35
    namespace "com.example.routermanager"

    defaultConfig {
        def commitCount = providers.exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
        }.standardOutput.asText.map { it.trim().toInteger() }

        def gitSha = providers.exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
        }.standardOutput.asText.map { it.trim() }

        def count = commitCount.getOrElse(1)
        def index = count - 1
        def major = index.intdiv(100)
        def minor = index.intdiv(10) % 10
        def patch = index % 10 + 1

        applicationId "com.example.routermanager"
        minSdk 21
        targetSdk 35
        versionCode count
        versionName "v${major}.${minor.toString().padLeft(2, '0')}.${patch.toString().padLeft(2, '0')}-${gitSha.getOrElse('dev')}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true // Enable shrinking and obfuscation
            shrinkResources true // Remove unused resources (e.g., drawables, layouts)

            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'
            ), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
